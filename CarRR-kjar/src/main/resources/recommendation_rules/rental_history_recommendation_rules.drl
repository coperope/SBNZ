//created on: May 18, 2020
package recommendation_rules

//list any import classes here.
import main.facts.Customer;
import main.facts.RentalRecommendations;
import main.facts.Category;
import main.facts.Tag;
import main.facts.Brand;
import main.facts.Fuel;
import main.facts.Transmission;
import main.facts.Vehicle;
import main.facts.RentalHistory;

rule "Rental history brand rule"
no-loop true
lock-on-active true
    when
        Customer($rentalHistory: rentalHistory)
        RentalHistory($rentalBrands: brands) from $rentalHistory

        $rr: RentalRecommendations()
        $vehicle: Vehicle($brand: brand)
    then
        modify ($rr) {addHits($vehicle, $rentalBrands.get($brand))}


end

rule "Rental history category rule"
no-loop true
lock-on-active true
    when
        Customer($rentalHistory: rentalHistory)
        RentalHistory($rentalCategories: categories) from $rentalHistory

        $rr: RentalRecommendations()
        $vehicle: Vehicle($categories: categories)
        Number($preferencesHit : intValue) from accumulate(
            $c: Category() from $categories
            ,
            init(int count = 0;),
            action(count += ($rentalCategories.get($c)!=null) ? (int)$rentalCategories.get($c) : 0;),
            result(count)
        )
    then
        modify ($rr) {addHits($vehicle, $preferencesHit)}
end

rule "Rental history tag rule"
no-loop true
lock-on-active true
    when
        Customer($rentalHistory: rentalHistory)
        RentalHistory($rentalTags: tags) from $rentalHistory

        $rr: RentalRecommendations()
        $vehicle: Vehicle($tags: tags)
        Number($preferencesHit : intValue) from accumulate(
            $t: Tag() from $tags
            ,
            init(int count = 0;),
            action(count += ($rentalTags.get($t)!=null) ? (int)$rentalTags.get($t) : 0;),
            result(count)
        )
    then
        modify ($rr) {addHits($vehicle, $preferencesHit)}
end

